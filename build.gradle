buildscript {
    // Make sure to use the same AGP as the main project.
    def androidGradlePlugin = '4.1.2'
    /**
     * Calculates the right lint version for the provided [androidGradlePlugin] version. AGP is
     * tightly coupled with lint because it implements LintClient. The formula to calculate the
     * lint version relative to AGP is: AGP version + 23.
     *
     * E.g. AGP version 3.2.1 -> Lint version 26.2.1
     */
    def lintVersion = { agp ->
        def (majorVersion, minorVersion, patchVersion) = agp.split("\\.")
        int lintMajorVersion = (majorVersion as Integer) + 23
        return "$lintMajorVersion.$minorVersion.$patchVersion"
    }
    ext.versions = [
        androidGradlePlugin     : androidGradlePlugin,
        androidLint             : lintVersion(androidGradlePlugin),
        compileSdk              : 30,
        minSdk                  : 14,
        targetSdk               : 30,
        kotlin                  : '1.4.21',
        kotlinCoroutines        : '1.4.2',
        kotlinMetadataJvm       : '0.2.0',
        androidxAppcompat       : '1.2.0',
        androidxConstraintlayout: '2.0.4',
        androidxFragment        : '1.2.5',
        androidxLifecycle       : '2.2.0',
        androidxNavigation      : '2.3.3',
        androidxRoom            : '2.2.6',
        androidxTestCore        : '1.3.0',
        androidxTestRunner      : '1.3.0',
        androidxTestJunit       : '1.1.2',
        androidxTestEspresso    : '3.3.0',
        autoService             : '1.0-rc7',
        okhttp                  : '3.12.13',
        dagger                  : '2.31.2',
        kotlinPoet              : '1.7.2',
        thrift                  : '0.12.0',
        mockito                 : '2.28.2',
        mockitoKotlin           : '2.2.0',
        mockk                   : '1.10.0',
        robolectric             : '4.4',
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

allprojects {
    group = 'com.linecorp.lich'
    repositories {
        google()
        jcenter()
    }
    afterEvaluate {
        if (ext.has('publication')) {
            afterEvaluate {
                configurePublication(project, ext.publication)
            }
        }
    }
}

def configurePublication(project, publication) {
    configure(project) {
        switch (publication.type) {
            case 'jar':
                task sourcesJar(type: Jar) {
                    archiveClassifier = 'sources'
                    from sourceSets.main.allSource
                }
                publishing {
                    publications {
                        myPublication(MavenPublication) {
                            from components.java
                            artifact sourcesJar
                        }
                    }
                }
                break
            case 'aar':
                task sourcesJar(type: Jar) {
                    archiveClassifier = 'sources'
                    from android.sourceSets.main.java.srcDirs
                }
                publishing {
                    publications {
                        myPublication(MavenPublication) {
                            from components.release
                            artifact sourcesJar
                        }
                    }
                }
                break
            default:
                throw new InvalidUserDataException("Unknown publication type: $publication.type")
        }
        publishing {
            publications {
                myPublication(MavenPublication) {
                    pom {
                        name = publication.name
                        description = publication.description
                        url = publication.url
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                name = 'LINE Corporation'
                                email = 'dl_oss_dev@linecorp.com'
                                url = 'https://engineering.linecorp.com/en/'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/line/lich.git'
                            developerConnection = 'scm:git:https://github.com/line/lich.git'
                            url = 'https://github.com/line/lich'
                        }
                        issueManagement {
                            system = 'GitHub'
                            url = 'https://github.com/line/lich/issues'
                        }
                    }
                }
            }
            repositories {
                maven {
                    url = findProperty('publishingRepositoryUrl') ?: "$rootDir/repo"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task cleanRepo(type: Delete) {
    delete "$rootDir/repo"
}
